pipeline TrainstopsPipeline {

    TrainstopsHTTPExtractor
    -> TrainstopsTextInterpreter
    -> TrainstopsCSVInterpreter
    -> TrainstopsTableInterpreter
    -> TrainstopsSQLLoader;

    block TrainstopsHTTPExtractor oftype HttpExtractor {
        url:"https://download-data.deutschebahn.com/static/datasets/haltestellen/D_Bahnhof_2020_alle.CSV";
    }
    block TrainstopsTextInterpreter oftype TextFileInterpreter { }

    block TrainstopsCSVInterpreter oftype CSVInterpreter {
        delimiter:';';
    }

    block TrainstopsTableInterpreter oftype TableInterpreter {
        header: true;
        columns: [
            "EVA_NR" oftype integer,
            "DS100" oftype text,
            "IFOPT" oftype IFOPT,
            "NAME" oftype text,
            "Verkehr" oftype VerkehrType,
            "Laenge" oftype Coordinate,
            "Breite" oftype Coordinate,
            "Betreiber_Name" oftype text,
            "Betreiber_Nr" oftype integer
        ];
    }

    block TrainstopsSQLLoader oftype SQLiteLoader{
        table: "trainstops";
        file: "trainstops.sqlite";
    }

}

valuetype VerkehrType oftype text {
    constraints:[OnlyValidVerkehrValues];
}

constraint OnlyValidVerkehrValues oftype AllowlistConstraint{
    allowlist: ["FV","RV","nur DPN"];
}

valuetype Coordinate oftype decimal {
    constraints:[OnlyValidCoordinates];
}

constraint OnlyValidCoordinates oftype RangeConstraint {
    lowerBound:-90.0;
    upperBound:90;
}


valuetype IFOPT oftype text {
    constraints:[OnlyValidIFOPT];
}

constraint OnlyValidIFOPT oftype RegexConstraint {
    regex: /^(..:[0-9]*:[0-9]*)(:[0-9]*){0,1}$/;
}

